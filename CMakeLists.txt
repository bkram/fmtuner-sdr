cmake_minimum_required(VERSION 3.15)
project(fm-tuner-sdr VERSION 1.0.0 LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

include_directories(${CMAKE_SOURCE_DIR}/include)
include(CheckCXXCompilerFlag)

option(FM_TUNER_ENABLE_X86_AVX2 "Enable AVX2/FMA optimizations on x86 (disable for portable binaries)" OFF)

find_package(OpenSSL REQUIRED)
find_package(Threads REQUIRED)
find_package(PkgConfig QUIET)

set(ALSA_FOUND FALSE)
if(PkgConfig_FOUND)
    pkg_check_modules(PC_ALSA IMPORTED_TARGET alsa)
    if(PC_ALSA_FOUND)
        set(ALSA_FOUND TRUE)
        set(ALSA_INCLUDE_DIRS ${PC_ALSA_INCLUDE_DIRS})
        set(ALSA_LIBRARIES ${PC_ALSA_LINK_LIBRARIES})
        message(STATUS "ALSA found: ${ALSA_LIBRARIES}")
    endif()
endif()

if(NOT ALSA_FOUND)
    find_path(ALSA_INCLUDE_DIRS NAMES alsa/asoundlib.h HINTS /usr/include /usr/local/include)
    find_library(ALSA_LIBRARIES NAMES asound HINTS /usr/lib /usr/local/lib)
    if(ALSA_INCLUDE_DIRS AND ALSA_LIBRARIES)
        set(ALSA_FOUND TRUE)
        message(STATUS "ALSA found (fallback): ${ALSA_LIBRARIES}")
    endif()
endif()

set(PORTAUDIO_INCLUDE_DIR "")
set(PORTAUDIO_LIBRARY "")

find_path(PORTAUDIO_INCLUDE_DIR
    NAMES portaudio.h
    HINTS /opt/homebrew/include /usr/local/include
)

find_library(PORTAUDIO_LIBRARY
    NAMES portaudio portaudio_static
    HINTS /opt/homebrew/lib /usr/local/lib
)

if((NOT PORTAUDIO_INCLUDE_DIR OR NOT PORTAUDIO_LIBRARY) AND PkgConfig_FOUND)
    pkg_check_modules(PC_PORTAUDIO QUIET IMPORTED_TARGET portaudio-2.0)
    if(PC_PORTAUDIO_FOUND)
        if(NOT PORTAUDIO_INCLUDE_DIR)
            set(PORTAUDIO_INCLUDE_DIR ${PC_PORTAUDIO_INCLUDE_DIRS})
        endif()
        if(NOT PORTAUDIO_LIBRARY)
            if(DEFINED pkgcfg_lib_PC_PORTAUDIO_portaudio)
                set(PORTAUDIO_LIBRARY ${pkgcfg_lib_PC_PORTAUDIO_portaudio})
            endif()
        endif()
    endif()
endif()

if(APPLE AND (NOT PORTAUDIO_INCLUDE_DIR OR NOT PORTAUDIO_LIBRARY))
    execute_process(
        COMMAND brew --prefix portaudio
        OUTPUT_VARIABLE PORTAUDIO_BREW_PREFIX
        RESULT_VARIABLE PORTAUDIO_BREW_RESULT
        OUTPUT_STRIP_TRAILING_WHITESPACE
        ERROR_QUIET
    )
    if(PORTAUDIO_BREW_RESULT EQUAL 0 AND PORTAUDIO_BREW_PREFIX)
        if(NOT PORTAUDIO_INCLUDE_DIR)
            find_path(PORTAUDIO_INCLUDE_DIR
                NAMES portaudio.h
                HINTS ${PORTAUDIO_BREW_PREFIX}/include
            )
        endif()
        if(NOT PORTAUDIO_LIBRARY)
            find_library(PORTAUDIO_LIBRARY
                NAMES portaudio portaudio_static
                HINTS ${PORTAUDIO_BREW_PREFIX}/lib
            )
        endif()
    endif()
endif()

if((NOT PORTAUDIO_INCLUDE_DIR OR NOT PORTAUDIO_LIBRARY) AND NOT TARGET PkgConfig::PC_PORTAUDIO)
    message(FATAL_ERROR "PortAudio not found. Install PortAudio or set PORTAUDIO_INCLUDE_DIR and PORTAUDIO_LIBRARY.")
endif()

set(SOURCES
    src/rtl_tcp_client.cpp
    src/fm_demod.cpp
    src/stereo_decoder.cpp
    src/af_post_processor.cpp
    src/cpu_features.cpp
    src/rds_decoder.cpp
    src/xdr_server.cpp
    src/audio_output.cpp
    src/config.cpp
    src/main.cpp
)

add_executable(fm-tuner-sdr ${SOURCES})
target_compile_definitions(fm-tuner-sdr PRIVATE FM_TUNER_SDR_VERSION="${PROJECT_VERSION}")

if(FM_TUNER_ENABLE_X86_AVX2)
    if(CMAKE_SYSTEM_PROCESSOR MATCHES "^(x86_64|AMD64|i[3-6]86)$" OR CMAKE_CXX_COMPILER_ID MATCHES "GNU|Clang")
        if(MSVC)
            target_compile_options(fm-tuner-sdr PRIVATE /arch:AVX2)
            message(STATUS "x86 build: enabling AVX2 via /arch:AVX2")
        else()
            target_compile_options(fm-tuner-sdr PRIVATE -mavx2 -mfma)
            message(STATUS "x86 build: enabling AVX2/FMA compile flags (-mavx2 -mfma) for portable binary")
        endif()
    else()
        message(STATUS "Non-x86 target (${CMAKE_SYSTEM_PROCESSOR}): skipping AVX2/FMA compile flags")
    endif()
endif()

target_include_directories(fm-tuner-sdr PRIVATE 
    ${CMAKE_SOURCE_DIR}/include
)
if(PORTAUDIO_INCLUDE_DIR)
    target_include_directories(fm-tuner-sdr PRIVATE ${PORTAUDIO_INCLUDE_DIR})
endif()

target_link_libraries(fm-tuner-sdr PRIVATE
    Threads::Threads
    OpenSSL::Crypto
    OpenSSL::SSL
)
if(ALSA_FOUND)
    target_include_directories(fm-tuner-sdr PRIVATE ${ALSA_INCLUDE_DIRS})
    target_link_libraries(fm-tuner-sdr PRIVATE ${ALSA_LIBRARIES})
    target_compile_definitions(fm-tuner-sdr PRIVATE FM_TUNER_HAS_ALSA=1)
endif()
if(TARGET PkgConfig::PC_PORTAUDIO)
    target_link_libraries(fm-tuner-sdr PRIVATE PkgConfig::PC_PORTAUDIO)
elseif(PORTAUDIO_LIBRARY)
    target_link_libraries(fm-tuner-sdr PRIVATE ${PORTAUDIO_LIBRARY})
endif()

install(TARGETS fm-tuner-sdr DESTINATION bin)
